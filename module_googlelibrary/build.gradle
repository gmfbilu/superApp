if (isModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

Properties properties = new Properties()
properties.load(project.rootProject.file("gradle.properties").newDataInputStream())
String versionNameP = properties.getProperty("versionName")
int versionCodeP = Integer.parseInt(properties.getProperty("versionCode"))

android {
    resourcePrefix "module_googlelibrary_"
    compileSdkVersion compileSdk as int
    defaultConfig {
        minSdkVersion minSdk as int
        targetSdkVersion targetSdk as int
        versionCode versionCodeP
        versionName versionNameP
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ moduleName : project.getName() ]
            }
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            if (isModule.toBoolean()) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                //集成开发模式下排除debug文件夹中的所有Java文件
                java {
                    exclude 'debug/**'
                }
            }

            res.srcDirs =
                    [
                            'src/main/res/layouts/activities',
                            'src/main/res/layouts/fragments',
                            'src/main/res/layouts/recycleviews',
                            'src/main/res/layouts/contents',
                            'src/main/res/layouts',
                            'src/main/res'
                    ]
        }

        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':lib_base')
    annotationProcessor "com.alibaba:arouter-compiler:${android_arouter_compiler_version}"

}
