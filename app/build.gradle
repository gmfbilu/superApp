apply plugin: 'com.android.application'
apply from: 'tinker-support.gradle'
apply plugin: 'channel'

Properties properties = new Properties()
properties.load(project.rootProject.file("gradle.properties").newDataInputStream())
String versionNameP = properties.getProperty("versionName")
int versionCodeP = Integer.parseInt(properties.getProperty("versionCode"))

static def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

//根据已有基础包重新生成多渠道包,gradle rebuildChannel
rebuildChannel {
    //指定渠道文件
    channelFile = file("..channel.txt")
    //已有Debug APK
    baseDebugApk = new File(project.projectDir, "baseApk/debug.apk")
    //已有Release APK，注意此目录位于app/baseApk
    baseReleaseApk = new File(project.projectDir, "baseApk/release.apk")
    //Debug渠道包输出目录
    debugOutputDir = new File(project.buildDir, "rebuildChannel/debug")
    //Release渠道包输出目录
    releaseOutputDir = new File(project.buildDir, "rebuildChannel/release")
    isFastMode = false
}


android {

    signingConfigs {
        debug {
            keyAlias DEBUG_KEYALIAS
            keyPassword DEBUG_KEYPASSWORD
            storeFile file(DEBUG_KEYFILE)
            storePassword DEBUG_KEYPASSWORD
            v1SigningEnabled true
            v2SigningEnabled true
        }
        release {
            keyAlias RELASE_KEYALIAS
            keyPassword RELASE_KEYPASSWORD
            storeFile file(RELASE_KEYFILE)
            storePassword RELASE_KEYPASSWORD
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }


    compileSdkVersion compileSdk as int
    defaultConfig {
        applicationId applicationId
        minSdkVersion minSdk as int
        targetSdkVersion targetSdk as int
        versionCode versionCodeP
        versionName versionNameP
        signingConfig signingConfigs.debug
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
        ndk {
            //设置支持的SO库架构
            // abiFilters 'armeabi' //, 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        jumboMode = true
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            versionNameSuffix "-debug"
            zipAlignEnabled false
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled false
            shrinkResources false
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }

    /*
    设置此目录的情况下so文件就要放在libs下
    sourceSets{
           main{
               jniLibs.srcDirs=['libs']
           }
       }*/

}



dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:multidex:${android_multidex_lib_version}"
    annotationProcessor "com.alibaba:arouter-compiler:${android_arouter_compiler_version}"
    implementation project(':lib_base')
    if (!isModule.toBoolean()) {
        implementation project(':module_main')
        implementation project(':module_test')
        implementation project(':module_fragment')
        implementation project(':module_googlelibrary')
        implementation project(':module_view')
        implementation project(':module_util')
        implementation project(':module_java')
    }
}
